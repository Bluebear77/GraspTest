PREFIX orkgp: <http://orkg.org/orkg/predicate/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT
  ?label
  ?score
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR=";;;") AS ?synonyms)
  ?id
  (GROUP_CONCAT(DISTINCT ?info; SEPARATOR=";;;") AS ?infos)
WHERE {
  {
    SELECT ?id ?label (COUNT(?id) AS ?score)
    WHERE {
      ?id rdfs:label ?label .
      ?id ?p ?o
    }
    GROUP BY ?id ?label
  }
  BIND("" AS ?alias)
  OPTIONAL {
    {
      ?id rdf:type/rdfs:label ?type_label.
      BIND(CONCAT("has type ", ?type_label) AS ?info)
    }
    UNION
    { ?id orkgp:description ?info }
  }
}
GROUP BY ?label ?score ?id
ORDER BY DESC(?score)
