PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

SELECT
  ?label
  ?score
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR=";;;") AS ?synonyms)
  ?id
  (GROUP_CONCAT(DISTINCT ?info; SEPARATOR=";;;") AS ?infos)
WHERE {
  { ?id @en@rdfs:label ?label }
  MINUS { ?id wdt:P31/wdt:P279* wd:Q17442446 }
  OPTIONAL { ?id skos:altLabel ?alias FILTER(LANG(?alias) = "en") }
  OPTIONAL { ?id ^schema:about/wikibase:sitelinks ?score }
  OPTIONAL {
    { ?id schema:description ?info FILTER(LANG(?info) = "en") }
    UNION
    {
      ?id wdt:P279/rdfs:label ?subclass_label .
      FILTER(LANG(?subclass_label) = "en")
      BIND(CONCAT("is subclass of ", ?subclass_label) AS ?info)
    }
    UNION
    {
      ?id wdt:P31/rdfs:label ?inst_label .
      FILTER(LANG(?inst_label) = "en")
      BIND(CONCAT("is instance of ", ?inst_label) AS ?info)
    }
    UNION
    { 
      ?id wdt:P106/rdfs:label ?info
      FILTER(LANG(?info) = "en")
      BIND(CONCAT("has occupation ", ?info) AS ?info)
    }
  }
}
GROUP BY ?label ?score ?id
ORDER BY DESC(?score)
