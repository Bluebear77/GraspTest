PREFIX fb: <http://rdf.freebase.com/ns/>

SELECT
  ?id
  (SAMPLE(?label) AS ?main_label)
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR=";;;") AS ?aliases)
WHERE {
  # Get ids and their frequencies for score
  {
    SELECT ?id (SUM(?freq) AS ?total_freq) WHERE {
      {
        SELECT ?id ("SUB" AS ?pos) (COUNT(?id) AS ?freq) WHERE {
          ?id fb:type.object.name ?label . 
          FILTER(LANG(?label) = "en") .
          ?id ?p ?o 
        } GROUP BY ?id 
      } UNION {
        SELECT ?id ("OBJ" AS ?pos) (COUNT(?id) AS ?freq) WHERE {
          ?id fb:type.object.name ?label . 
          FILTER(LANG(?label) = "en") .
          ?s ?p ?id .
          FILTER(ISIRI(?id)) 
        } GROUP BY ?id
      }
    } GROUP BY ?id
  }

  # Get label
  ?id fb:type.object.name ?label .
  FILTER(LANG(?label) = "en") .

  # Get aliases
  OPTIONAL {
    ?id fb:common.topic.alias ?alias
    FILTER(LANG(?alias) = "en") 
  }
}
GROUP BY ?id
ORDER BY DESC(MAX(?total_freq))
