PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fb: <http://rdf.freebase.com/ns/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT
  ?label
  ?score
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR=";;;") AS ?synonyms)
  ?id
  (GROUP_CONCAT(DISTINCT ?info; SEPARATOR=";;;") AS ?infos)
WHERE {
  {
    SELECT ?id (COUNT(?id) AS ?score)
    WHERE { ?s ?id ?o }
    GROUP BY ?id
  }
  ?id fb:type.object.name ?label .
  FILTER(LANG(?label) = "en") .
  ?id fb:type.object.type fb:type.property .
  OPTIONAL { ?id fb:common.topic.alias ?alias FILTER(LANG(?alias) = "en") }
  OPTIONAL {
    {
      ?id fb:type.property.schema/rdfs:label ?schema_label .
      FILTER(LANG(?schema_label) = "en")
      BIND(CONCAT("is part of ", ?schema_label, " schema") AS ?info)
    }
    UNION
    {
      ?id fb:type.property.expected_type/rdfs:label ?type_label .
      FILTER(LANG(?type_label) = "en")
      BIND(CONCAT("links to type ", ?type_label) AS ?info)
    }
  }
}
GROUP BY ?label ?score ?id
ORDER BY DESC(?score)
