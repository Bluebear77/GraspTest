PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT
  (SAMPLE(?label) AS ?main_label)
  (SUM(?freq) AS ?score)
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR=";;;") AS ?synonyms)
  ?id
  (GROUP_CONCAT(DISTINCT ?info; SEPARATOR=";;;") AS ?infos)
WHERE {
  # Get ids and their frequencies for score
  { SELECT ?id (COUNT(?id) AS ?freq) WHERE { ?id ?p ?o } GROUP BY ?id } 
  UNION
  { SELECT ?id (COUNT(?id) AS ?freq) WHERE { ?s ?p ?id . FILTER(ISIRI(?id)) } GROUP BY ?id }

  # Get label
  OPTIONAL { ?id rdfs:label ?label_en FILTER(LANG(?label_en) = "en") }
  OPTIONAL { ?id rdfs:label ?label_plain FILTER(LANG(?label_plain) = "" || DATATYPE(?label_plain) = xsd:string) }
  BIND(COALESCE(?label_en, ?label_plain) AS ?label)

  # Get aliases
  OPTIONAL { ?id skos:altLabel ?alias_en FILTER(LANG(?alias_en) = "en") }
  OPTIONAL { ?id skos:altLabel ?alias_plain FILTER(LANG(?alias_plain) = "" || DATATYPE(?alias_plain) = xsd:string) }
  BIND(COALESCE(?alias_en, ?alias_plain) AS ?alias)

  # Get additional information, such as comment, domain, range, type, and superclass
  OPTIONAL {
    {
      OPTIONAL { ?id rdfs:comment ?comment_en FILTER(LANG(?comment_en) = "en") }
      OPTIONAL { ?id rdfs:comment ?comment_plain FILTER(LANG(?comment_plain) = "" || DATATYPE(?comment_plain) = xsd:string) }
      BIND(COALESCE(?comment_en, ?comment_plain) AS ?info)
    } UNION {
      ?id rdfs:domain ?d .
      OPTIONAL { ?d rdfs:label ?domain_en FILTER(LANG(?domain_en) = "en") }
      OPTIONAL { ?d rdfs:label ?domain_plain FILTER(LANG(?domain_plain) = "" || DATATYPE(?domain_plain) = xsd:string) }
      BIND(COALESCE(?domain_en, ?domain_plain) AS ?domain)
      BIND(CONCAT("has domain ", ?domain) AS ?info)
    } UNION {
      ?id rdfs:range ?r .
      OPTIONAL { ?r rdfs:label ?range_en FILTER(LANG(?range_en) = "en") }
      OPTIONAL { ?r rdfs:label ?range_plain FILTER(LANG(?range_plain) = "" || DATATYPE(?range_plain) = xsd:string) }
      BIND(COALESCE(?range_en, ?range_plain) AS ?range)
      BIND(CONCAT("has range ", ?range) AS ?info)
    } UNION {
      ?id rdf:type ?t .
      OPTIONAL { ?t rdfs:label ?type_en FILTER(LANG(?type_en) = "en") }
      OPTIONAL { ?t rdfs:label ?type_plain FILTER(LANG(?type_plain) = "" || DATATYPE(?type_plain) = xsd:string) }
      BIND(COALESCE(?type_en, ?type_plain) AS ?type)
      BIND(CONCAT("is a ", ?type) AS ?info)
    } UNION {
      ?id rdfs:subClassOf ?sc .
      OPTIONAL { ?sc rdfs:label ?superclass_en FILTER(LANG(?superclass_en) = "en") }
      OPTIONAL { ?sc rdfs:label ?superclass_plain FILTER(LANG(?superclass_plain) = "" || DATATYPE(?superclass_plain) = xsd:string) }
      BIND(COALESCE(?superclass_en, ?superclass_plain) AS ?superclass)
      BIND(CONCAT("is subclass of ", ?superclass) AS ?info) 
    }
  }
}
GROUP BY ?id
ORDER BY DESC(?score)
